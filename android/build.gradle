import org.eclipse.jgit.api.Git

group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.7.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.eclipse.jgit:org.eclipse.jgit:5.12.0+"
    }
}

rootProject.allprojects {
    repositories {
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17' //, '-frelaxed-template-template-args'
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    ndkVersion = "22.1.7171670"
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "CMakeLists.txt"
        }
    }
}

void cloneThirdPartyRepo(String name, String uri, String commit) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)
    org.eclipse.jgit.api.Git repo;

//    if (dir.exists()) {
//        repo = org.eclipse.jgit.api.Git.open(dir)
//        def status = repo.status().call();
//        def head = repo.log().setMaxCount(1).call().iterator().next();
//
//        if (!status.clean || head.name != commit) {
//            dir.deleteDir();
//        }
//    }

    if (!dir.exists()) {
        dir.mkdirs()
        repo =
                Git.cloneRepository()
                        .setDirectory(dir)
                        .setURI(uri)
                        .setRemote('origin')
                        .setCloneSubmodules(true)
                        .call()

        repo.checkout().setName(commit).call()
        repo.submoduleUpdate()
        repo.submoduleInit()
    }
}

task cloneThirdPartyRepos {
    cloneThirdPartyRepo('AudioKit', 'https://github.com/AudioKit/AudioKit', '818ee7f9e4b00627b9959de8e6b73c47f8b3cd09')
    cloneThirdPartyRepo('libnyquist', 'https://github.com/ddiakopoulos/libnyquist.git', '244afdd52e8fc5bc0396cbcd9437b2d74271509f')
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', 'd25993c5dbe4750cd8d51a3729b6def6f9d20d77')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', 'bf574519e601202c3a9d27a74f345921277eed39')
}

preBuild.dependsOn cloneThirdPartyRepos

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
